#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WIDTH  800
#define HEIGHT 600
#define PRT_CNT WIDTH * HEIGHT
#define EPS 0.001
#define WORKGROUP_SIZE 32
layout (local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1 ) in;

struct Particle {
	vec2 anchor;
	vec2 pos;
	vec2 vel;
	vec3 color;
};

layout(std140, binding = 0) buffer buf
{
   Particle parts[];
};

layout(std140, set = 1, binding = 1) uniform UniformBufferObject {
    vec2 mouse_pos;
    float dt;
} ubo;

void main() {
	uint id = gl_GlobalInvocationID.x;
	if (id > PRT_CNT) {
		return;
	}

	Particle pt = parts[id];

	vec2 delta_anchor = pt.anchor - pt.pos;
	vec2 delta_mouse = ubo.mouse_pos - pt.pos;

	float l_da = length(delta_anchor);
	float l_dm = length(delta_mouse);

	if (l_da > 0) {
		pt.vel += delta_anchor / l_da / 20;
	}

	if (l_dm > 0.01) {
		delta_mouse = delta_mouse / pow(l_dm, 2);
		pt.vel -= delta_mouse / 70;
	}

	
	pt.pos += pt.vel * ubo.dt * 0.5;
	pt.vel *= 0.99;

	if (l_da < EPS && length(pt.vel) < 0.1) {
		pt.pos = pt.anchor;
		pt.vel = vec2(0, 0);
	}
	

	parts[id] = pt;
}